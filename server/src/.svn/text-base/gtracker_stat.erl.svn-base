-module(gtracker_stat).

-behaviour(mds_gen_server).

-include("common_defs.hrl").

-export([start/1, stop/0, on_start/1, on_stop/2, on_msg/3, on_amsg/2, on_info/2]).

-import(mds_utils, [get_param/2, get_param/3]).
-import(gtracker_common, [join_pg/2, leave_pg/2, send_pg/2, bin_to_urlencoded/1]).

-define(MOD, {local, ?MODULE}).

-record(dev_info, {did, last_coord = undef}).
-record(state, {gt_pgroup, dev_cache}).

start(Opts) ->
   mds_gen_server:start(?MOD, Opts).

stop() ->
   mds_gen_server:stop(?MOD).

on_start(Opts) ->
   inets:start(),
   SelfOpts = get_param(self, Opts),
   PGroup = get_param(gt_pgroup, SelfOpts, ?DEF_GT_PGROUP),
   join_pg(PGroup, self()),
   {ok, #state{gt_pgroup = PGroup, dev_cache = ets:new(dev_cache, [set, {keypos, 2}])}}.

on_stop(Reason, #state{gt_pgroup = PGroup}) ->
   leave_pg(PGroup, self()),
   log(info, "Stopped <~p>.", [Reason]),
   ok.

on_msg(Msg, _From, State) ->
   log(info, "Unknown sync received ~p", [Msg]),
   {reply, ok, State}.

on_amsg(Msg, State) ->
   log(info, "Unknown async message ~p.", [Msg]),
   {noreply, State}.

on_info(?MSG(From, _, _), State) when From =:= self() -> % ignore self messages
   {noreply, State};

on_info(?MSG(_From, _GroupName, {coord, first, Did, {_Lat, _Lon, _Timestamp} = Coord}), #state{dev_cache = DevCache} = State) ->
   ets:update_element(DevCache, Did, {?FieldId(dev_info, last_coord), Coord}),
   {noreply, State};

on_info(?MSG(_From, _GroupName, {coord, Did, {_Lat, _Lon, _Timestamp} = Coord}),
   #state{gt_pgroup = PgGroup, dev_cache = DevCache} = State) ->
   [#dev_info{last_coord = LC}] = ets:lookup(DevCache, Did),
   Speed = nmea_utils:calc_speed(LC, Coord),
   log(debug, "Coord1 = ~p, Coord2 = ~p, Speed = ~p", [LC, Coord, Speed]),
   send_pg(PgGroup, {speed, Did, Speed}),
   ets:update_element(DevCache, Did, {?FieldId(dev_info, last_coord), Coord}),
   {noreply, State};

on_info(?MSG(_From, _GroupName, {online, Did}), #state{dev_cache = DevCache} = State) ->
   ets:insert(DevCache, #dev_info{did = Did}),
   log(info, "~p was inserted into cache.", [Did]),
   {noreply, State};

on_info(?MSG(_From, _GroupName, {offline, Did}), #state{dev_cache = DevCache} = State) ->
   ets:delete(DevCache, Did),
   log(info, "~p was removed from cache.", [Did]),
   {noreply, State};

on_info(Msg, State) ->
   log(info, "Unknown info received ~p", [Msg]),
   {noreply, State}.

%=======================================================================================================================
%  log helpers
%=======================================================================================================================
log(LogLevel, Format, Data) ->
   mds_gen_server:log(?MODULE, LogLevel, Format, Data).

log(LogLevel, Text) ->
   mds_gen_server:log(?MODULE, LogLevel, Text).

%=======================================================================================================================
%  unit testing facilities
%=======================================================================================================================
-ifdef(EUNIT).
-include_lib("eunit/include/eunit.hrl").

field_id_test() ->
   ?assertEqual(2, ?FieldId(dev_info, did)),
   ?assertEqual(3, ?FieldId(dev_info, last_coord)).

-endif.
